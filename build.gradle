plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
}

group='io.spoud.kafka.oauth'
version='1.2-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven")
    }
    gradlePluginPortal()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.9.0'
    implementation group: 'io.confluent', name: 'kafka-schema-registry-client', version: '7.9.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation 'org.bitbucket.b_c:jose4j:0.9.6'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'com.azure:azure-identity:1.12.2'
    implementation 'com.gradleup.shadow:shadow-gradle-plugin:8.3.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
}

test {
    useJUnitPlatform()
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}


publishing {
  publications {
    shadow(MavenPublication) {
      from components.shadow
      artifactId = 'extensions-shadow'
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/spoud/kafka-oauth-extensions"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}