plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'maven-publish'
}

group='io.spoud.kafka.oauth'
version='1.4-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url = uri("https://packages.confluent.io/maven")
    }
    gradlePluginPortal()
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '4.1.1'
    implementation(group: 'io.confluent', name: 'kafka-schema-registry-client', version: '8.1.1') {
        exclude group: 'org.apache.kafka', module: 'kafka-clients'
    }
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.21.0'
    implementation 'org.bitbucket.b_c:jose4j:0.9.6'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'com.azure:azure-identity:1.18.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.3'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.21.0'
}

test {
    useJUnitPlatform()
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}


publishing {
  publications {
    shadow(MavenPublication) {
      from components.shadow
      artifactId = 'extensions-shadow'
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/spoud/kafka-oauth-extensions"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}